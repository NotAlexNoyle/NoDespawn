plugins {

    id "java" // Tell gradle this is a java project.
    id "com.github.johnrengelman.shadow" version "7.1.2" // Import utility to package libraries into .jar file.
    
}

group = "net.trueog.nodespawnog" // Declare package name.
version = "2.0" // Declare package version.

def apiVersion = "1.18.2" // Declare minecraft server target version.
def targetJavaVersion = 17 // Declare java compatibility minimum.

apply plugin: "eclipse" // Enable integration with Eclipse IDE for development.

processResources {

    def props = [
        version: version,
        apiVersion: apiVersion,
    ]

    inputs.properties props // Indicates to rerun if version changes.

    filesMatching("plugin.yml") {

        expand props

    }

}

repositories {

    mavenCentral()

    maven {

        url "https://repo.purpurmc.org/snapshots" // Get purpur API from purpur maven repository.

	}

	maven {

		name = "CodeMC"
        url = uri("https://repo.codemc.org/repository/maven-public/") // Get NBT-API from codemc maven repository.

    }

}

dependencies {

    compileOnly "org.purpurmc.purpur:purpur-api:1.18.2-R0.1-SNAPSHOT" // Declare purpur API version to be packaged.

	implementation("de.tr7zw:item-nbt-api:2.11.1") // Declare NBT-API version to be packaged.

}

shadowJar {

    minimize()
    
    relocate 'de.tr7zw', 'net.trueog.nodespawnog.item-nbt-api' // Prevent conflicts with other NBT-API utilizing plugins.
    
}

jar.dependsOn shadowJar

tasks.jar.configure {

    classifier = "part"
    
}

tasks.shadowJar.configure {

    classifier = null

}

compileJava {

    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'

}